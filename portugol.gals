#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = SLR
#RegularDefinitions
  
#Tokens
:[\ \n\t\r\s]+ | [/][/].* | "/*"([^"*"]|[\r\n]|("*"+([^"*"/]|[\r\n])))*"*"+/
"":\s

// comentários
COMENTARIO_UMA_LINHA:[/][/].*
COMENTARIO_VARIAS_LINHAS: "/*"([^"*"]|[\r\n]|("*"+([^"*"/]|[\r\n])))*"*"+/

// delimitadores
"{":\{
"}":\}
"(":\(
")":\)
"[":\[
"]":\]
",":,
";":;

// operações matemáticas
"+": \+
"-":\-
"*":\*
"/": /
"%": "%"
// atribuição
"=":=
"-->":"-->"
"+=":"+="
"*=":"*="
"/=":"/="
"-=":"-="
// unitarios
"++":"++"
"--":"--"

// reservadas
PROGRAMA: programa
FUNCAO: funcao
CONST: const
RETORNE: retorne
INCLUA: inclua
BIBLIOTECA: biblioteca
LEIA: leia
ESCREVA: escreva

// declarações
TIPO_INTEIRO: "inteiro"
TIPO_REAL: real
TIPO_CARACTER: caracter
TIPO_CADEIA: cadeia
TIPO_LOGICO: logico

VALOR_LOGICO: verdadeiro|falso

// desvios condicionais
SE: "se"
SENAO: "senao"
ESCOLHA: "escolha"
CASO: "caso"
PARE: "pare"
":": :

// laços de repetição
FACA: "faca"
ENQUANTO: "enquanto"
PARA: "para"

// operações lógicas
E: e
OU: ou
NAO: nao

// nome variavel
ID: [a-zA-Z][a-zA-Z0-9_]*

// operações bitwise
"&": &
"|": \|
"~": ~
"^": \^
"<<": "<<"
">>": ">>"

// operações relacionais
"<=":<=
"<":<
">=":>=
">":>
"==":==
"!=":!=

// tipos
VALOR_INTEIRO: [0-9]+
VALOR_REAL: [0-9]+\.[0-9]+
VALOR_CARACTER: '[^']'
VALOR_CADEIA:\"([^\"](\\\")*)*\"

#NonTerminals
<PROGRAMA>
<PROGRAMA_VAZIO>
<PROGRAMA_SEM_BIBLIOTECA>
<PROGRAMA_COM_BIBLIOTECA>
<LISTA_BIBLIOTECAS>
<BIBLIOTECA>
<BIBLIOTECA_SEM_ALIAS>
<BIBLIOTECA_COM_ALIAS>
<LISTA_DECLARACOES>
<DECLARACAO>
<DECLARACAO_VARIAVEL>
<TIPO>
<LISTA_VARIAVEIS>
<LISTA_VARIAVEIS_COM_ATRIBUICAO>
<VARIAVEL>
<VARIAVEL_SEM_ATRIBUICAO>
<VARIAVEL_COM_ATRIBUICAO>
<ATRIBUIDOR>
<AUTO_ATRIBUIDOR>
<VARIAVEL_VETOR>
<VARIAVEL_VETOR_COM_TAMANHO>
<VARIAVEL_VETOR_SEM_TAMANHO>
<INICIALIZACAO_VETOR>
<LISTA_ELEMENTOS_VETOR>
<ELEMENTO_VETOR>
<DECLARACAO_FUNCAO>
<TIPO_RETORNO_FUNCAO>
<LISTA_PARAMETROS>
<PARAMETRO>
<PARAMETRO_POR_VALOR>
<PARAMETRO_POR_REFERENCIA>
<LISTA_ELEMENTOS_FUNCAO>
<ELEMENTO_FUNCAO>
<ESTRUTURA_CONTROLE>
<CHAMADA_FUNCAO>
<SE>
<SE_SIMPLES>
<SENAO>
<SE_SENAO>
<ESCOLHA_CASO>
<PARAMETRO_ESCOLHA_CASO>
<LISTA_CASOS>
<CASO>
<ENQUANTO>
<PARAMETRO_ENQUANTO>
<FACA_ENQUANTO>
<PARA>
<PARA_PARTE_1>
<PARA_PARTE_2>
<PARA_PARTE_3>

<LISTA_ARGUMENTOS>
<ARGUMENTO>

<exp>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>

<op_rel>
<op_neg>
<op_bitwise>
<op_arit_baixa>
<op_arit_alta>
<op_arit_mais_alta>

#Grammar
<PROGRAMA> ::= <PROGRAMA_SEM_BIBLIOTECA> | <PROGRAMA_COM_BIBLIOTECA> | <PROGRAMA_VAZIO>;
<PROGRAMA_VAZIO> ::= PROGRAMA  #8 #0 #51"{"  "}" #7;
<PROGRAMA_COM_BIBLIOTECA> ::= PROGRAMA  #8 #0  #51 "{" <LISTA_BIBLIOTECAS><LISTA_DECLARACOES> "}" #7;
<PROGRAMA_SEM_BIBLIOTECA> ::= PROGRAMA  #8 #0 #51 "{" <LISTA_DECLARACOES> "}" #7;
<LISTA_BIBLIOTECAS> ::= <BIBLIOTECA><LISTA_BIBLIOTECAS> | <BIBLIOTECA>;
<BIBLIOTECA> ::= <BIBLIOTECA_COM_ALIAS> | <BIBLIOTECA_SEM_ALIAS>;
<BIBLIOTECA_COM_ALIAS> ::= INCLUA BIBLIOTECA ID "-->" ID;
<BIBLIOTECA_SEM_ALIAS> ::= INCLUA BIBLIOTECA ID;
<LISTA_DECLARACOES> ::= <DECLARACAO><LISTA_DECLARACOES> | <DECLARACAO>;
<DECLARACAO> ::= <DECLARACAO_VARIAVEL> | <DECLARACAO_FUNCAO>;

<DECLARACAO_VARIAVEL>::= <TIPO>#1 #100 <LISTA_VARIAVEIS> | CONST #4 <TIPO>#1 #100 <LISTA_VARIAVEIS_COM_ATRIBUICAO>;
<TIPO> ::= TIPO_INTEIRO | TIPO_REAL | TIPO_CADEIA | TIPO_LOGICO | TIPO_CARACTER;
<LISTA_VARIAVEIS> ::= <VARIAVEL> "," <LISTA_VARIAVEIS> | <VARIAVEL>;
<VARIAVEL> ::= <VARIAVEL_COM_ATRIBUICAO> | <VARIAVEL_SEM_ATRIBUICAO> #23;
<LISTA_VARIAVEIS_COM_ATRIBUICAO> ::= <VARIAVEL_COM_ATRIBUICAO> "," <LISTA_VARIAVEIS_COM_ATRIBUICAO> | <VARIAVEL_COM_ATRIBUICAO>;

<VARIAVEL_SEM_ATRIBUICAO> ::= <VARIAVEL_VETOR> | ID #2;
<VARIAVEL_COM_ATRIBUICAO> ::=  <VARIAVEL_SEM_ATRIBUICAO> #600 <ATRIBUIDOR> #200 <exp> #41 | <VARIAVEL_SEM_ATRIBUICAO> <AUTO_ATRIBUIDOR>;

<ATRIBUIDOR> ::= "=" | "+=" | "-=" | "*=" | "/=";
<AUTO_ATRIBUIDOR> ::= "++" | "--";
<VARIAVEL_VETOR>::= <VARIAVEL_VETOR_COM_TAMANHO> | <VARIAVEL_VETOR_SEM_TAMANHO>;
<VARIAVEL_VETOR_COM_TAMANHO> ::= ID #2 "[" #13 VALOR_INTEIRO #14 #800 "]" | ID #2 "[" #13 VALOR_INTEIRO #14 #800 "]" "[" VALOR_INTEIRO "]";
<VARIAVEL_VETOR_SEM_TAMANHO> ::= ID #2 "["#13 "]" | ID #2 "["#13 "]" "[" "]";

<INICIALIZACAO_VETOR> ::=    "{" <LISTA_ELEMENTOS_VETOR> "}";

<LISTA_ELEMENTOS_VETOR> ::= <ELEMENTO_VETOR> "," <LISTA_ELEMENTOS_VETOR> | <ELEMENTO_VETOR>; //50
<ELEMENTO_VETOR> ::= <exp>;

<DECLARACAO_FUNCAO> ::=  
	  FUNCAO #101 ID #0 #8 #5 "(" <LISTA_PARAMETROS> ")" "{" #11 "}" #7
	| FUNCAO #101 ID #0 #8 #5 "(" <LISTA_PARAMETROS> ")" "{" #11 <LISTA_ELEMENTOS_FUNCAO> "}" #7
	| FUNCAO #101 <TIPO_RETORNO_FUNCAO> #1 ID #0 #8 #5 "(" #11 <LISTA_PARAMETROS> ")" "{" "}" #7
	| FUNCAO #101 <TIPO_RETORNO_FUNCAO> #1 ID #0 #8 #5 "(" #11 <LISTA_PARAMETROS> ")" "{" <LISTA_ELEMENTOS_FUNCAO> "}"  #7;

<TIPO_RETORNO_FUNCAO> ::= <TIPO> | <TIPO> "[" "]";
<LISTA_PARAMETROS> ::= <PARAMETRO> #22 "," <LISTA_PARAMETROS> | <PARAMETRO> #22 | î;
<PARAMETRO> ::= <PARAMETRO_POR_VALOR> | <PARAMETRO_POR_REFERENCIA>;
<PARAMETRO_POR_VALOR> ::= <TIPO> #1 ID #2 | <TIPO> #1 <VARIAVEL_VETOR_SEM_TAMANHO>;
<PARAMETRO_POR_REFERENCIA> ::= <TIPO> #1 "&"#71 ID #2 | <TIPO> #1 "&"#71 <VARIAVEL_VETOR_SEM_TAMANHO>;
<LISTA_ELEMENTOS_FUNCAO> ::= <ELEMENTO_FUNCAO><LISTA_ELEMENTOS_FUNCAO> | <ELEMENTO_FUNCAO>;
<ELEMENTO_FUNCAO> ::= <DECLARACAO_VARIAVEL> 
	| <VARIAVEL> 
	| <ESTRUTURA_CONTROLE> 
	| <CHAMADA_FUNCAO> 
	| RETORNE <exp> 
	| RETORNE <CHAMADA_FUNCAO>
	| PARE;
<ESTRUTURA_CONTROLE> ::= <SE> | <ESCOLHA_CASO> | <ENQUANTO> | <FACA_ENQUANTO> | <PARA>;
<SE> ::= <SE_SIMPLES> | <SENAO>;
<SE_SIMPLES> ::=
	  SE #8 #9 #910 "(" <exp> #913 ")" <ELEMENTO_FUNCAO> #10 #7
 	| SE #8 #9 #910 "(" <exp> #913 ")" "{" <LISTA_ELEMENTOS_FUNCAO> "}" #10 #7
	| SE #8 #9 #910 "(" <exp> #913 ")" "{" "}" #10 #7;
<SENAO> ::= SENAO #9 #916 <ELEMENTO_FUNCAO> #917 #10 |  SENAO #9 #916 "{" <LISTA_ELEMENTOS_FUNCAO> "}" #10 #917 |  SENAO #9 #916 "{" "}" #917 #10;
<ESCOLHA_CASO> ::= ESCOLHA #8 #9 "(" <PARAMETRO_ESCOLHA_CASO> ")" "{" <LISTA_CASOS> "}" #10;
<PARAMETRO_ESCOLHA_CASO> ::= ID #2 | <VARIAVEL_VETOR_COM_TAMANHO> | VALOR_INTEIRO | VALOR_CARACTER | <CHAMADA_FUNCAO>;
<LISTA_CASOS> ::= <CASO><LISTA_CASOS> | <CASO>;
<CASO> ::= CASO #92 <PARAMETRO_ESCOLHA_CASO> ":" | CASO #92 <PARAMETRO_ESCOLHA_CASO> ":" <LISTA_ELEMENTOS_FUNCAO>; 

<ENQUANTO> ::= 
	   ENQUANTO #8 #9 "(" <PARAMETRO_ENQUANTO> ")" <ELEMENTO_FUNCAO> #10
	|  ENQUANTO #8 #9 "(" <PARAMETRO_ENQUANTO> ")" "{" "}" #10
	|  ENQUANTO #8 #9 "(" <PARAMETRO_ENQUANTO> ")" "{" <LISTA_ELEMENTOS_FUNCAO> "}" #10;

<PARAMETRO_ENQUANTO> ::= <exp> | <CHAMADA_FUNCAO>;

<FACA_ENQUANTO> ::=
	  FACA #8 #9 "{" "}" ENQUANTO "(" <PARAMETRO_ENQUANTO> ")" #10
	| FACA #8 #9 <ELEMENTO_FUNCAO> ENQUANTO "(" <PARAMETRO_ENQUANTO> ")" #10
	| FACA #8 #9 "{" <LISTA_ELEMENTOS_FUNCAO> "}" ENQUANTO "(" <PARAMETRO_ENQUANTO> ")" #10;
<PARA> ::=
	  PARA #8 #9 "(" <PARA_PARTE_1> ";"#3 <PARA_PARTE_2> ";" <PARA_PARTE_3> ")" "{" "}" #10
	| PARA #8 #9 "(" <PARA_PARTE_1> ";"#3 <PARA_PARTE_2> ";" <PARA_PARTE_3> ")" <ELEMENTO_FUNCAO> #10
	| PARA #8 #9 "(" <PARA_PARTE_1> ";"#3 <PARA_PARTE_2> ";" <PARA_PARTE_3> ")" "{"  <LISTA_ELEMENTOS_FUNCAO> "}" #10;
<PARA_PARTE_1> ::= <TIPO>#1 #100<VARIAVEL> | <VARIAVEL> | î;
<PARA_PARTE_2> ::= <exp> | <CHAMADA_FUNCAO>;
<PARA_PARTE_3> ::= <exp> | <VARIAVEL_COM_ATRIBUICAO> | î;

<CHAMADA_FUNCAO> ::= ID #99 "("<LISTA_ARGUMENTOS>")" | LEIA #300 "(" <exp> #301 ")" | ESCREVA #400  "("<LISTA_ARGUMENTOS>")";
<LISTA_ARGUMENTOS> ::= <ARGUMENTO> "," <LISTA_ARGUMENTOS> | <ARGUMENTO> #302 | î;
<ARGUMENTO> ::= <exp>;

<exp>  ::= <exp> OU <exp1> | <exp1>;
<exp1> ::= <exp1> E <exp2> | <exp2>;
<exp2> ::= <exp2> <op_rel> #915 <exp3> | <exp3>;
<exp3> ::= <exp3> <op_bitwise> <exp4> | <exp4>;
<exp4> ::= <exp4> <op_arit_baixa> <exp5> | <exp5>;
<exp5> ::= <exp5> <op_arit_alta> <exp6> | <exp6> ;
<exp6> ::= <op_neg> <exp7> | <exp7>;

<exp7> ::= VALOR_INTEIRO #601 #912 | VALOR_REAL | VALOR_LOGICO |  VALOR_CADEIA | VALOR_CARACTER | <INICIALIZACAO_VETOR> | <VARIAVEL_SEM_ATRIBUICAO> #6 #912 | "(" <exp> ")";

<op_rel> ::= ">" | "<" | ">=" | "<=" | "==" | "!="; 
<op_neg> ::= "-" #502 | NAO;
<op_bitwise> ::= "<<" | ">>" | "|" | "&" | "^" | "~";
<op_arit_baixa> ::= "+" #500 | "-" #501;
<op_arit_alta> ::= "*" | "/" | "%";
